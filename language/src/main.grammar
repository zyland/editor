//!element

@top Tree { expr lineEnd }

@precedence {
  concat @left,
  mul @left,
  add @left,
  logic @left,
  def @left
}

expr {
    Identifier
  | String
  //| Concat { expr !concat endExpr }
  | BinExpr {
      expr !mul(Mul | Div) endExpr
    | expr !add(Add | Sub | Join) endExpr
    | expr !logic(Or | And) endExpr
  }
  | DefExpr {
    Identifier !def(Def) endExpr
  }
  | LogicBlock { LogicLine {!logic (Or | And) expr lineEnd}+ }
  | "(" expr ")"
}
endExpr {
    expr
  | block
}

block { lineEnd indent expr (dedent | eof) }

//!skip

@skip {
  spaces |
  Comment |
  blankLineStart (spaces | Comment)* lineEnd
}

//!lineEnd

lineEnd { newline | eof }

//!context

@context trackIndent from "./tokens.js"

//!externalTokens

@external tokens indentation from "./tokens.js" {
  indent
  dedent
  blankLineStart
}

//!tokens

@tokens {
  spaces { $[ \t]+ }
  newline { "\n" }
  eof { @eof }
  Comment { "#" ![\n]+ }
  Identifier { $[a-zA-Z0-9_]+ }
  String { '"' !["]* '"' }
  Add { "+" }
  Sub { "-" }
  Mul { "*" }
  Div { "/" }
  Or { "|" }
  And { "&" }
  Join { "\\" }
  Def { ":" }
}