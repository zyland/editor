//!element

@top Tree { expr }

@precedence {
  call @left,
  mul @left,
  add @left,
  logic @left,
  def @left,
  lines @left
}

expr {
    nonLine
  | line
  | Lines { line !lines expr }
  | "(" expr ")"
}

nonLine {
    Identifier
  | String
  | Call { nonLine !call expr }
  | BinExpr {
      expr !mul(Mul | Div) expr
    | expr !add(Add | Sub | Join) expr
    | expr !logic(Or | And) expr
    | expr !def(Def) expr
  }
}

line {
    expr newline
  | Call { expr block }
  | BinExpr {
      expr !mul(Mul | Div) block
    | expr !add(Add | Sub | Join) block
    | expr !logic(Or | And) block
    | expr !def(Def) block
  }
}

block {
  newline indent blockable (dedent | eof)
}

blockable {
    expr newline
  | LogicBlock {
      LogicLine { !logic (Or | And) expr (newline | eof)? }+
  }
}

//!skip

@skip {
  spaces |
  Comment |
  blankLineStart (spaces | Comment)* lineEnd
}

//!lineEnd

lineEnd { newline | eof }

//!context

@context trackIndent from "./tokens.js"

//!externalTokens

@external tokens indentation from "./tokens.js" {
  indent
  dedent
  blankLineStart
}

//!tokens
@tokens {
  spaces { $[ \t]+ }
  newline { "\n" }
  eof { @eof }
  Comment { "//" ![\n]+ }
  Identifier { $[a-zA-Z0-9_]+ }
  String { '"' !["]* '"' }
  Add { "+" }
  Sub { "-" }
  Mul { "*" }
  Div { "/" ![/] }
  Or { "|" }
  And { "&" }
  Join { "\\" }
  Def { ":" }
}